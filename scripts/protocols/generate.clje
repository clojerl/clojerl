(ns protocols.generate
  (:require [protocols.utils :as utils]
            [clojure.string :as str]))

(def primitive-types
  '{"clojerl.String"    {:pred "erlang:is_binary"
                         :order 1}
    "clojerl.BitString" {:pred "erlang:is_bitstring"
                         :order 2}
    "clojerl.Integer"   {:pred "erlang:is_integer"
                         :order 3}
    "clojerl.Float"     {:pred "erlang:is_float"
                         :order 4}
    "clojerl.Boolean"   {:pred "erlang:is_boolean"
                         :order 5}
    "erlang.List"       {:pred "erlang:is_list"
                         :order 6}
    "erlang.Map"        {:pred "erlang:is_map"
                         :order 7}
    "erlang.Tuple"      {:pred "erlang:is_tuple"
                         :order 8}
    "erlang.Fn"         {:pred "erlang:is_function"
                         :order 9}
    "clojerl.Nil"       {:pred nil
                         :order 10}
    "clojerl.Keyword"   {:pred "erlang:is_atom"
                         :order 11}
    "erlang.Port"       {:pred "erlang:is_port"
                         :order 12}
    "erlang.Process"    {:pred "erlang:is_pid"
                         :order 13}
    "erlang.Reference"  {:pred "erlang:is_reference"
                         :order 14}})

(def primitive-type? primitive-types)

(def protocols
  '{clojerl.IAssociative   [contains_key  [Assoc Key]
                            entry_at      [Assoc Key]
                            assoc         [Assoc Key Value]]
    clojerl.IChunk         [drop_first    [Chunk]]
    clojerl.IChunkedSeq    [chunked_first [Seq]
                            chunked_next  [Seq]
                            chunked_more  [Seq]]
    clojerl.IColl          [cons          [Coll, Item]
                            empty         [Coll]]
    clojerl.ICounted       [count         [Seq]]
    clojerl.IDeref         [deref         [Ref]]
    clojerl.IEquiv         [equiv         [X Y]]
    clojerl.IErl           [->erl         [X Recursive]]
    clojerl.IError         [message       [Error]]
    clojerl.IExceptionInfo [data          [ExInfo]
                            cause         [ExInfo]]
    clojerl.IFn            [apply         [Fn Args]]
    clojerl.IHash          [hash          [X]]
    clojerl.IIndexed       [nth           [Coll N]
                            nth           [Coll N NotFound]]
    clojerl.ILookup        [get           [X Key]
                            get           [X Key NotFound]]
    clojerl.IMap           [keys          [Map]
                            vals          [Map]
                            without       [Map Key]]
    clojerl.IMeta          [meta          [X]
                            with_meta     [X Meta]]
    clojerl.INamed         [name          [X]
                            namespace     [X]]
    clojerl.IRecord        [_             [_]]
    clojerl.IReduce        [reduce        [Coll Fun]
                            reduce        [Coll Fun Init]]
    clojerl.IReference     [alter_meta    [Ref Fun Args]
                            reset_meta    [Ref Meta]]
    clojerl.IReversible    [rseq          [Seq]]
    clojerl.ISeq           [first         [Seq]
                            next          [Seq]
                            more          [Seq]]
    clojerl.ISeqable       [seq           [X]
                            to_list       [X]]
    clojerl.ISequential    [_             [_]]
    clojerl.ISet           [disjoin       [Coll Item]
                            contains      [Coll Item]]
    clojerl.ISorted        [_             [_]]
    clojerl.IStack         [peek          [Stack]
                            pop           [Stack]]
    clojerl.IStringable    [str           [X]]
    clojerl.IType          [_             [_]]

    ;; Erlang IO protocols
    erlang.io.IWriter         [write     [Writer Str]
                               write     [Writer Format Value]]
    erlang.io.IReader         [read      [Reader]
                               read      [Reader Length]
                               read_line [Reader]
                               skip      [Reader N]]
    erlang.io.IPushbackReader [unread    [Reader Ch]]
    erlang.io.ICloseable      [close     [X]]
    })

(def UNDERSCORE '_)

(def CATCH-ALL ::catch-all)

(def NIL-TYPE "clojerl.Nil")
(def STRING-TYPE "clojerl.String")
(def BOOLEAN-TYPE "clojerl.Boolean")

(defn underscore?
  [x]
  (= x UNDERSCORE))

(defn keyword-type?
  [t]
  (= t "clojerl.Keyword"))

(defn proto-function-head
  [f args]
  (let [args-str (apply str (interpose ", " args))]
    (str "'" f "'(" args-str ") ->\n"
         "  case " (first args) " of\n")))

(defn pattern-and-guard
  [type]
  (cond
    (= CATCH-ALL type)
    "_"

    (underscore? type)
    "#{?TYPE := _}"

    (= NIL-TYPE type)
    "?NIL"

    (primitive-type? type)
    (str "X_ when " (get-in primitive-types [type :pred]) "(X_)")

    :else
    (str "#{?TYPE := '" type "'}")))

(defn not-implemented-clause
  [f args type]
  (str "    " (pattern-and-guard type) " ->\n"
       "      clj_protocol:not_implemented(?MODULE, '" f "', " (first args) ")"))

(defn implementation-call-clause
  [f args type]
  (let [args-str (apply str (interpose ", " args))]
    (str "    " (pattern-and-guard type) " ->\n"
         "      '" type "':'" f "'(" args-str ");\n")))

(defn proto-function-tail
  [f args]
  (str (not-implemented-clause f args CATCH-ALL) "\n"
       "  end.\n\n"))

(defn proto-function-clause
  [f args [type implemented?]]
  (if implemented?
    (implementation-call-clause f args type)
    (str (not-implemented-clause f args type) ";\n")))

(defn proto-function
  [proto-name f args types-specs]
  (when-not (underscore? f)
    (str
     (proto-function-head f args)
     (->> types-specs
          (map (partial proto-function-clause f args))
          (apply str))
     (proto-function-tail f args))))

(defn proto-functions
  [proto-name fns types-specs]
  (->> fns
       (partition 2)
       (map (fn [[f args]]
              (proto-function proto-name f args types-specs)))
       (apply str)))

(defn satisfies?-clause
  [[type implemented?]]
  (str "    " (pattern-and-guard type) " ->  " implemented?  ";\n"))

(defn satisfies?-function
  [types]
  (str "?SATISFIES(X) ->\n"
       "  case X of\n"
       (->> types
            (map satisfies?-clause)
            (apply str))
       "    _ -> false\n"
       "  end.\n"))

(defn extends?-clause
  [[type implemented?]]
  (when implemented?
    (str "    '" type "' -> true;\n")))

(defn extends?-function
  [types]
  (str "?EXTENDS(X) ->\n"
       "  case X of\n"
       (->> types
            (map extends?-clause)
            (apply str))
       "    _ -> false\n"
       "  end.\n"))

(defn exports
  [fns]
  (->> fns
       (partition 2)
       (keep (fn [[f args]]
               (when-not (underscore? f)
                 (str "'" f "'/" (count args)))))
       (interpose ", ")
       (apply str)))

(defn callbacks
  [fns]
  (->> fns
       (partition 2)
       (map (fn [[f args]]
              (str "-callback '" f "'("
                   (apply str
                          (interpose ", " (repeat (count args) "any()")))
                   ") -> any().\n")))
       (apply str)))

(defn type-ordering
  [[type _]]
  (if (underscore? type)
    0
    (-> (primitive-types type {:order -1})
        :order)))

(defn module
  [proto-name fns]
  (let [types (->> proto-name
                   utils/who-implements
                   (map #(vector % true))
                   (into {UNDERSCORE      false
                          ;; is_atom will return true for boolean and
                          ;; undefined (i.e. nil)
                          BOOLEAN-TYPE    false
                          NIL-TYPE        false
                          ;; is_binary and is_bitstring might return
                          ;; true for the same value so we add the
                          ;; default clause for no implementation
                          STRING-TYPE     false})
                   (sort-by type-ordering))]
    (str "-module('" proto-name "').\n\n"
         "-include(\"clojerl.hrl\").\n"
         "-include(\"clojerl_int.hrl\").\n\n"
         "-clojure(true).\n"
         "-protocol(true).\n\n"
         (let [ex (exports fns)]
           (when-not (empty? ex)
             (str "-export([" ex "]).\n")))
         "-export([?SATISFIES/1]).\n"
         "-export([?EXTENDS/1]).\n\n"
         (callbacks fns)
         "\n"
         (proto-functions proto-name fns types)
         (satisfies?-function types)
         "\n"
         (extends?-function types))))

(defn write-module
  [content proto-name]
  (let [path (if (str/starts-with? (str proto-name) "clojerl.")
               "src/erl/lang/protocols/"
               "src/erl/erlang/")]
    (println "Writing" (str path proto-name ".erl"))
    (spit (str path proto-name ".erl") content)))

(defn write-protocols
  [protocols]
  (doseq [[proto-name fns] protocols]
    (-> proto-name
        (module fns)
        ;; println
        (write-module proto-name)
        )))

(defn -main []
  (write-protocols protocols))
