;   Copyright (c) Rich Hickey. All rights reserved.
;   The use and distribution terms for this software are covered by the
;   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
;   which can be found in the file epl-v10.html at the root of this distribution.
;   By using this software in any fashion, you are agreeing to be bound by
;   the terms of this license.
;   You must not remove this notice, or any other, from this software.

; Author: Stuart Halloway

(ns clojure.test-clojure.protocols
  (:use clojure.test clojure.test-clojure.protocols.examples)
  (:require [clojure.test-clojure.protocols.more-examples :as other]
            [clojure.set :as set]
            [clojure.string :as str]
            clojure.test-helper)
  (:import [clojure.test-clojure.protocols.examples
            ExampleProtocol
            MarkerProtocol
            MarkerProtocol2]))

(defn delete-module [module]
  (doto module
    code/purge.e
    code/delete.e
    (-> code/which.e file/delete.e)))

(defn find-protocol-impls
  [protocol]
  (let [type->str #(subs (str %) 1)
        prefix (type->str protocol)
        f (fn [[module path]]
            (str/starts-with? (type->str module) prefix))]
    (->> (code/all_loaded.e)
         (filter f)
         (map first))))

(defn clean-protocol [protocol]
  (doseq [m (find-protocol-impls protocol)]
    (delete-module m)))

;; temporary hack until I decide how to cleanly reload protocol
;; this no longer works
(defn reload-example-protocols
  []
  ;; (clean-protocol ExampleProtocol)
  ;; (clean-protocol other/SimpleProtocol)

  ;; (alter-var-root #'clojure.test-clojure.protocols.examples/ExampleProtocol
  ;;                 assoc :impls {})
  ;; (alter-var-root #'clojure.test-clojure.protocols.more-examples/SimpleProtocol
  ;;                 assoc :impls {})

  (require :reload
           'clojure.test-clojure.protocols.examples
           'clojure.test-clojure.protocols.more-examples))

(defn method-names
  "return sorted list of method names on a module"
  [m]
  (->> (erlang/get_module_info.e m :functions)
       (map (comp str first))
       (map #(subs % 1))
       (filter (comp not #{"$_clj_on_load" "module_info" "module_info"}))
       (filter #(not (str/ends-with? % "__val")))
       sort))

(defrecord EmptyRecord [])
(defrecord TestRecord [a b])
(defn r
  ([a b] (TestRecord. a b))
  ([a b meta ext] (TestRecord. a b meta ext)))

(defrecord MapEntry [k v])

(defn subset-map? [a b]
  (set/subset? (set a) (set b)))

(deftest protocols-test
  (testing "protocol fns have useful metadata"
    (let [common-meta {:ns 'clojure.test-clojure.protocols.examples
                       :protocol 'clojure.test-clojure.protocols.examples.ExampleProtocol}]
      (are [m f] (subset-map? (merge (quote m) common-meta)
                              (meta (var f)))
           {:name foo :arglists ([a]) :doc "method with one arg"} foo
           {:name bar :arglists ([a b]) :doc "method with two args"} bar
           {:name baz :arglists ([a] [a b]) :doc "method with multiple arities" :tag :clojerl.String} baz
           {:name with-quux :arglists ([a]) :doc "method name with a hyphen"} with-quux)))
  (testing "protocol fns throw IllegalArgumentException if no impl matches"
    (is (thrown-with-msg?
          :error
          #"No implementation of method: 'foo' of protocol: clojure.test-clojure.protocols.examples.ExampleProtocol found for type: clojerl.Integer"
          (foo 10))))
  (testing "protocols generate a corresponding interface using _ instead of - for method names"
    (is (= ["bar" "baz" "baz" "foo" "with-quux"] (method-names :clojure.test-clojure.protocols.examples))))
  (testing "error conditions checked when defining protocols"
    (is (thrown-with-msg?
         :throw
         #"Definition of function m in protocol badprotdef must take at least one arg."
         (eval '(defprotocol badprotdef (m [])))))
    (is (thrown-with-msg?
         :throw
         #"Function m in protocol badprotdef was redefined. Specify all arities in single definition."
         (eval '(defprotocol badprotdef (m [this arg]) (m [this arg1 arg2]))))))
  (testing "you can redefine a protocol with different methods"
    (eval '(defprotocol Elusive (old-method [x])))
    (eval '(defprotocol Elusive (new-method [x])))))

(deftype HasMarkers []
  ExampleProtocol
  (foo [this] "foo")
  MarkerProtocol
  MarkerProtocol2)

(deftype WillGetMarker []
  ExampleProtocol
  (foo [this] "foo"))

(extend-type WillGetMarker MarkerProtocol)

(deftest marker-tests
  (testing "That a marker protocol has no methods"
    (is (= '() (method-names :clojure.test-clojure.protocols.examples.MarkerProtocol))))
  (testing "That types with markers are reportedly satifying them."
    (let [hm (HasMarkers.)
          wgm (WillGetMarker.)]
      (is (satisfies? MarkerProtocol hm))
      (is (satisfies? MarkerProtocol2 hm))
      (is (satisfies? MarkerProtocol wgm)))))

(deftype ExtendTestWidget [name])
(deftype HasProtocolInline []
  ExampleProtocol
  (foo [this] :inline))

(deftest record-marker-interfaces
  (testing "record? and type? return expected result for IRecord and IType"
    (let [r (TestRecord. 1 2)]
      (is (record? r)))))

(deftype ExtendsTestWidget []
  ExampleProtocol)
#_(deftest extends?-test
  (reload-example-protocols)
  (testing "returns false if a type does not implement the protocol at all"
    (is (false? (extends? other/SimpleProtocol ExtendsTestWidget))))
  (testing "returns true if a type implements the protocol directly" ;; semantics changed 4/15/2010
    (is (true? (extends? ExampleProtocol ExtendsTestWidget)))))

(deftype SatisfiesTestWidget []
  ExampleProtocol)
#_(deftest satisifies?-test
  (reload-example-protocols)
  (let [whatzit (SatisfiesTestWidget.)]
    (testing "returns false if a type does not implement the protocol at all"
      (is (false? (satisfies? other/SimpleProtocol whatzit))))
    (testing "returns true if a type implements the protocol directly"
      (is (true? (satisfies? ExampleProtocol whatzit))))
    (testing "returns true if a type explicitly extends protocol"
      (extend-type SatisfiesTestWidget
        other/SimpleProtocol
        (foo [this] this))
      (is (true? (satisfies? other/SimpleProtocol whatzit))))))

(deftype ReExtendingTestWidget [])
#_(deftest re-extending-test
  (reload-example-protocols)
  (extend-type ReExtendingTestWidget
    ExampleProtocol
    (foo [_] "first foo")
    (baz [_] "first baz"))
  (testing "if you re-extend, the old implementation is replaced (not merged!)"
    (extend-type ReExtendingTestWidget
      ExampleProtocol
      (baz [_] "second baz")
      (bar [_ _] "second bar"))
    (let [whatzit (ReExtendingTestWidget.)]
      (is (thrown? :error (foo whatzit)))
      (is (= "second bar" (bar whatzit nil)))
      (is (= "second baz" (baz whatzit))))))

(defrecord DefrecordObjectMethodsWidgetA [a])
(defrecord DefrecordObjectMethodsWidgetB [a])
(deftest defrecord-object-methods-test
  (testing "= depends on fields and type"
    (is (true? (= (DefrecordObjectMethodsWidgetA. 1)
                  (DefrecordObjectMethodsWidgetA. 1))))
    (is (false? (= (DefrecordObjectMethodsWidgetA. 1)
                   (DefrecordObjectMethodsWidgetA. 2))))
    (is (false? (= (DefrecordObjectMethodsWidgetA. 1)
                   (DefrecordObjectMethodsWidgetB. 1))))))

(deftest defrecord-acts-like-a-map
  (let [rec (r 1 2)]
    (is (= (r 1 3 {} {:c 4}) (merge rec {:b 3 :c 4})))
    (is (= {:foo 1 :b 2} (set/rename-keys rec {:a :foo})))
    (is (= {:a 11 :b 2 :c 10} (merge-with + rec {:a 10 :c 10})))))

(deftest degenerate-defrecord-test
  (let [empty (EmptyRecord.)]
    (is (nil? (seq empty)))
    (is (not (.contains_key empty :a)))))

(deftest defrecord-protocols-test
  (testing "clojerl.ICounted"
    (let [rec (r 1 2)]
      (is (= 2 (.count rec)))))
  (testing "clojerl.IColl"
    (testing ".cons"
      (let [rec (r 1 2)]
        (are [x] (= rec (.cons rec x))
             nil {})
        (is (= (r 1 3) (.cons rec {:b 3})))
        (is (= (r 1 4) (.cons rec [:b 4]))))))
  (testing "clojerl.IEquiv"
    (let [rec (r 1 2)]
      (is (false? (.equiv rec {:a 1 :b 2})))
      (is (.equiv {:a 1 :b 2} rec))
      (is (.equiv rec (r 1 2)))))
  (testing "clojerl.Associated"
    (let [rec (r 1 2)]
      (is (.contains_key rec :a))
      (is (= [:a 1] (.entry_at rec :a)))
      (is (= {:a 1 :b 2 :c 3} (.assoc rec :c 3)))))
  (testing "clojerl.ISeqable"
    (let [rec (r 1 2)]
      (is (= #{[:a 1] [:b 2]} (set (.seq rec))))
      (is (= #{[:a 1] [:b 2] [:c 3]} (set (.seq (assoc rec :c 3)))))))
  (testing "clojerl.IMap"
    (let [rec (r 1 2)]
      (is (= #{:a :b} (set (.keys rec))))
      (is (= #{1 2} (set (.vals rec))))
      (is (= {:b 2} (.without rec :a))))))

(defrecord RecordWithSpecificFieldNames [this that k m o])
(deftest defrecord-with-specific-field-names
  (let [rec (RecordWithSpecificFieldNames. 1 2 3 4 5)]
    (is (= rec rec))
    (is (= 1 (:this (with-meta rec {:foo :bar}))))
    (is (= 3 (get rec :k)))
    (is (= (seq rec) '([:this 1] [:that 2] [:k 3] [:m 4] [:o 5])))
    (is (= (dissoc rec :k) {:this 1, :that 2, :m 4, :o 5}))))

(defrecord RecordToTestStatics1 [a])
(defrecord RecordToTestStatics2 [a b])
(defrecord RecordToTestStatics3 [a b c])
(defrecord RecordToTestBasis [a b c])
(defrecord RecordToTestBasisHinted [^clojerl.String a ^clojerl.Integer b c])
(defrecord RecordToTestHugeBasis [a b c d e f g h i j k l m n o p q r s t u v w x y z])
(defrecord TypeToTestBasis [a b c])
(defrecord TypeToTestBasisHinted [^clojerl.String a ^clojerl.Integer b c])

(deftest test-statics
  (testing "that a record has its generated static methods"
    (let [r1 (RecordToTestStatics1. 1)
          r2 (RecordToTestStatics2. 1 2)
          r3 (RecordToTestStatics3. 1 2 3)
          rn (RecordToTestStatics3. 1 nil nil)]
      (testing "that a record created with the ctor equals one by the static factory method"
        (is (= r1    (RecordToTestStatics1/create.e {:a 1})))
        (is (= r2    (RecordToTestStatics2/create.e {:a 1 :b 2})))
        (is (= r3    (RecordToTestStatics3/create.e {:a 1 :b 2 :c 3})))
        (is (= rn    (RecordToTestStatics3/create.e {:a 1}))))
      (testing "that a literal record equals one by the static factory method"
        (is (= #clojure.test-clojure.protocols.RecordToTestStatics1{:a 1} (RecordToTestStatics1/create.e {:a 1})))
        (is (= #clojure.test-clojure.protocols.RecordToTestStatics2{:a 1 :b 2} (RecordToTestStatics2/create.e {:a 1 :b 2})))
        (is (= #clojure.test-clojure.protocols.RecordToTestStatics3{:a 1 :b 2 :c 3} (RecordToTestStatics3/create.e {:a 1 :b 2 :c 3})))
        (is (= #clojure.test-clojure.protocols.RecordToTestStatics3{:a 1} (RecordToTestStatics3/create.e {:a 1})))
        (is (= #clojure.test-clojure.protocols.RecordToTestStatics3{:a 1 :b nil :c nil} (RecordToTestStatics3/create.e {:a 1}))))))
  (testing "that records and types have a sane generated basis method"
    (let [rb  (clojure.test-clojure.protocols.RecordToTestBasis/get_basis.e)
          rbh (clojure.test-clojure.protocols.RecordToTestBasisHinted/get_basis.e)
          rhg (clojure.test-clojure.protocols.RecordToTestHugeBasis/get_basis.e)
          tb  (clojure.test-clojure.protocols.TypeToTestBasis/get_basis.e)
          tbh (clojure.test-clojure.protocols.TypeToTestBasisHinted/get_basis.e)]
      (is (= '[a b c] rb))
      (is (= '[a b c] rb))
      (is (= '[a b c d e f g h i j k l m n o p q r s t u v w x y z] rhg))
      (testing "that record basis hinting looks as we expect"
        (is (= (:tag (meta (rbh 0))) 'clojerl.String))
        (is (= (:tag (meta (rbh 1))) 'clojerl.Integer))
        (is (nil? (:tag (meta (rbh 2))))))
      (testing "that type basis hinting looks as we expect"
        (is (= (:tag (meta (tbh 0))) 'clojerl.String))
        (is (= (:tag (meta (tbh 1))) 'clojerl.Integer))
        (is (nil? (:tag (meta (tbh 2)))))))))

(defrecord RecordToTestFactories [a b c])
(defrecord RecordToTestA [a])
(defrecord RecordToTestB [b])
(defrecord RecordToTestHugeFactories [a b c d e f g h i j k l m n o p q r s t u v w x y z])
(defrecord RecordToTestDegenerateFactories [])

(deftest test-record-factory-fns
  (testing "if the definition of a defrecord generates the appropriate factory functions"
    (let [r    (RecordToTestFactories. 1 2 3)
          r-n  (RecordToTestFactories. nil nil nil)
          huge (RecordToTestHugeFactories. 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26)
          r-a  (map->RecordToTestA {:a 1 :b 2})
          r-b  (map->RecordToTestB {:a 1 :b 2})
          r-d  (RecordToTestDegenerateFactories.)]
      (testing "that a record created with the ctor equals one by the positional factory fn"
        (is (= r    (->RecordToTestFactories 1 2 3)))
        (is (= huge (->RecordToTestHugeFactories 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26))))
      (testing "that a record created with the ctor equals one by the map-> factory fn"
        (is (= r    (map->RecordToTestFactories {:a 1 :b 2 :c 3})))
        (is (= r-n  (map->RecordToTestFactories {})))
        (is (= r    (map->RecordToTestFactories (map->RecordToTestFactories {:a 1 :b 2 :c 3}))))
        (is (= r-n  (map->RecordToTestFactories (map->RecordToTestFactories {}))))
        (is (= r-d  (map->RecordToTestDegenerateFactories {})))
        (is (= r-d  (map->RecordToTestDegenerateFactories
                     (map->RecordToTestDegenerateFactories {})))))
      (testing "that ext maps work correctly"
        (is (= (assoc r :xxx 42)  (map->RecordToTestFactories {:a 1 :b 2 :c 3 :xxx 42})))
        (is (= (assoc r :xxx 42)  (map->RecordToTestFactories (map->RecordToTestFactories
                                                               {:a 1 :b 2 :c 3 :xxx 42}))))
        (is (= (assoc r-n :xxx 42) (map->RecordToTestFactories {:xxx 42})))
        (is (= (assoc r-n :xxx 42) (map->RecordToTestFactories (map->RecordToTestFactories
  {:xxx 42}))))
        (is (= (assoc r-d :xxx 42) (map->RecordToTestDegenerateFactories {:xxx 42})))
        (is (= (assoc r-d :xxx 42) (map->RecordToTestDegenerateFactories
                                    (map->RecordToTestDegenerateFactories {:xxx 42})))))
      (testing "record equality"
        (is (not= r-a r-b))
        (is (= (into {} r-a) (into {} r-b)))
        ;; This fails in CLJS but not in CLJ
        ;; (is (not= (into {} r-a) r-b))
        (is (= (map->RecordToTestA {:a 1 :b 2})
               (map->RecordToTestA (map->RecordToTestB {:a 1 :b 2}))))
        (is (= (map->RecordToTestA {:a 1 :b 2 :c 3})
               (map->RecordToTestA (map->RecordToTestB {:a 1 :b 2 :c 3}))))
        (is (= (map->RecordToTestA {:a 1 :d 4})
               (map->RecordToTestA (map->RecordToTestDegenerateFactories {:a 1 :d 4}))))
        (is (= r-n (map->RecordToTestFactories (maps/new.e))))
        (is (= r-a (map->RecordToTestA (into {} r-b))))
        (is (= r-a (map->RecordToTestA r-b)))
        (is (not= r-a (map->RecordToTestB r-a)))
        (is (= r (assoc r-n :a 1 :b 2 :c 3)))
        (is (not= r-a (assoc r-n :a 1 :b 2)))
        (is (not= (assoc r-b :c 3 :d 4) (assoc r-n :a 1 :b 2 :c 3 :d 4)))
        (is (= (into {} (assoc r-b :c 3 :d 4)) (into {} (assoc r-n :a 1 :b 2 :c 3 :d 4))))
        (is (= (assoc r :d 4) (assoc r-n :a 1 :b 2 :c 3 :d 4))))
      (testing "that factory functions have docstrings"
        ;; just test non-nil to avoid overspecifiying what's in the docstring
        (is (false? (-> ->RecordToTestFactories var meta :doc nil?)))
        (is (false? (->  map->RecordToTestFactories var meta :doc nil?))))
      (testing "that a literal record equals one by the positional factory fn"
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a 1 :b 2 :c 3} (->RecordToTestFactories 1 2 3)))
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a 1 :b nil :c nil} (->RecordToTestFactories 1 nil nil)))
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a [] :b {} :c ()} (->RecordToTestFactories [] {} ()))))
      (testing "that a literal record equals one by the map-> factory fn"
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a 1 :b 2 :c 3} (map->RecordToTestFactories {:a 1 :b 2 :c 3})))
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a 1 :b nil :c nil} (map->RecordToTestFactories {:a 1})))
        (is (= #clojure.test-clojure.protocols.RecordToTestFactories{:a nil :b nil :c nil} (map->RecordToTestFactories {})))))))

(defn compare-huge-types
  [hugeL hugeR]
  (and
   (= (.-a hugeL) (.-a hugeR))
   (= (.-b hugeL) (.-b hugeR))
   (= (.-c hugeL) (.-c hugeR))
   (= (.-d hugeL) (.-d hugeR))
   (= (.-e hugeL) (.-e hugeR))
   (= (.-f hugeL) (.-f hugeR))
   (= (.-g hugeL) (.-g hugeR))
   (= (.-h hugeL) (.-h hugeR))
   (= (.-i hugeL) (.-i hugeR))
   (= (.-j hugeL) (.-j hugeR))
   (= (.-k hugeL) (.-k hugeR))
   (= (.-l hugeL) (.-l hugeR))
   (= (.-m hugeL) (.-m hugeR))
   (= (.-n hugeL) (.-n hugeR))
   (= (.-o hugeL) (.-o hugeR))
   (= (.-p hugeL) (.-p hugeR))
   (= (.-q hugeL) (.-q hugeR))
   (= (.-r hugeL) (.-r hugeR))
   (= (.-s hugeL) (.-s hugeR))
   (= (.-t hugeL) (.-t hugeR))
   (= (.-u hugeL) (.-u hugeR))
   (= (.-v hugeL) (.-v hugeR))
   (= (.-w hugeL) (.-w hugeR))
   (= (.-x hugeL) (.-x hugeR))
   (= (.-y hugeL) (.-y hugeR))
   (= (.-z hugeL) (.-z hugeR))))

(deftype TypeToTestFactory [a])
(defrecord TypeToTestHugeFactories [a b c d e f g h i j k l m n o p q r s t u v w x y z])

(deftest deftype-factory-fn
  (testing "that the ->T factory is gen'd for a deftype and that it works"
    (is (= (.-a (TypeToTestFactory. 42)) (.-a (->TypeToTestFactory 42))))
    (is (compare-huge-types
         (TypeToTestHugeFactories.  1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26)
         (->TypeToTestHugeFactories 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26))))
  (testing "that the generated factory checks arity constraints"
    (is (thrown? :throw (->TypeToTestHugeFactories 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25)))
    (is (thrown? :throw (->TypeToTestHugeFactories 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27)))))

(deftest test-ctor-literals
  (testing "that constructor literals work for deftypes too"
    (is (= (.-a (TypeToTestFactory. 42)) (.-a #clojure.test-clojure.protocols.TypeToTestFactory[42])))
    (is (compare-huge-types
         (TypeToTestHugeFactories.  1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26)
         #clojure.test-clojure.protocols.TypeToTestHugeFactories[1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26]))))

(defrecord RecordToTestLiterals [a])
(defrecord TestNode [v l r])
(deftype TypeToTestLiterals [a]
  clojerl.IStringable
  (str [_] (str "#TypeToTestLiterals[" a "]")))
(def lang-str "en")
(deftest exercise-literals
  (testing "that ctor literals can be used in common 'places'"
    (is (= (RecordToTestLiterals. ()) #clojure.test-clojure.protocols.RecordToTestLiterals[()]))
    (is (= (.-a (TypeToTestLiterals. ())) (.-a #clojure.test-clojure.protocols.TypeToTestLiterals[()])))
    (is (= (RecordToTestLiterals. 42) (into #clojure.test-clojure.protocols.RecordToTestLiterals[0] {:a 42})))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))  (RecordToTestLiterals. #clojure.test-clojure.protocols.RecordToTestLiterals[42])))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))  (->RecordToTestLiterals #clojure.test-clojure.protocols.RecordToTestLiterals[42])))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))
           #clojure.test-clojure.protocols.RecordToTestLiterals[#clojure.test-clojure.protocols.RecordToTestLiterals[42]]))
    (is (= (TestNode. 1
                      (TestNode. 2
                                 (TestNode. 3
                                            nil
                                            nil)
                                 nil)
                      (TestNode. 4
                                 (TestNode. 5
                                            (TestNode. 6
                                                       nil
                                                       nil)
                                            nil)
                                 (TestNode. 7
                                            nil
                                            nil)))
           #clojure.test-clojure.protocols.TestNode{:v 1
                                                    :l #clojure.test-clojure.protocols.TestNode{:v 2
                                                                                                :l #clojure.test-clojure.protocols.TestNode{:v 3 :l nil :r nil}
                                                                                                :r nil}
                                                    :r #clojure.test-clojure.protocols.TestNode{:v 4
                                                                                                :l #clojure.test-clojure.protocols.TestNode{:v 5
                                                                                                                                            :l #clojure.test-clojure.protocols.TestNode{:v 6 :l nil :r nil}
                                                                                                                                            :r nil}
                                                                                                :r #clojure.test-clojure.protocols.TestNode{:v 7 :l nil :r nil}}})))

  (testing "that records and types are evalable"
    (is (= (RecordToTestLiterals. 42) (eval #clojure.test-clojure.protocols.RecordToTestLiterals[42])))
    (is (= (RecordToTestLiterals. 42) (eval #clojure.test-clojure.protocols.RecordToTestLiterals{:a 42})))
    (is (= (RecordToTestLiterals. 42) (eval (RecordToTestLiterals. 42))))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))
           (eval #clojure.test-clojure.protocols.RecordToTestLiterals[#clojure.test-clojure.protocols.RecordToTestLiterals[42]])))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))
           (eval #clojure.test-clojure.protocols.RecordToTestLiterals[#clojure.test-clojure.protocols.RecordToTestLiterals{:a 42}])))
    (is (= (RecordToTestLiterals. (RecordToTestLiterals. 42))
           (eval #clojure.test-clojure.protocols.RecordToTestLiterals{:a #clojure.test-clojure.protocols.RecordToTestLiterals[42]})))
    (is (= 42 (.-a (eval #clojure.test-clojure.protocols.TypeToTestLiterals[42])))))

  (testing "that ctor literals only work with constants or statics"
    (is (thrown? :error (read-string "#clojerl.String[(str 'en)]")))
    (is (thrown? :error (read-string "(let [s \"en\"] #clojerl.String[(str 'en)])")))
    (is (thrown? :error (read-string "#clojure.test-clojure.protocols.RecordToTestLiterals{(keyword \"a\") 42}"))))

  (testing "that ctors can have whitespace after class name but before {"
    (is (= (RecordToTestLiterals. 42)
           (read-string "#clojure.test-clojure.protocols.RecordToTestLiterals   {:a 42}"))))

  (testing "that the correct errors are thrown with malformed literals"
    (is (thrown-with-msg?
          :error
          #"Unreadable constructor form.*"
          (read-string "#clojerl.String(\"en\")")))
    (is (thrown-with-msg?
          :error
          #"Unexpected number of constructor arguments.*"
          (read-string "#clojerl.String[\"\" \"\" \"\" \"\"]")))
    (is (thrown? :error (read-string "#erlang.util.Nachos(\"en\")")))))

(defrecord RecordToTestPrinting [a b])
(deftest defrecord-printing
  (testing "that the default printer gives the proper representation"
    (let [r   (RecordToTestPrinting. 1 2)]
      (is (= "#clojure.test-clojure.protocols.RecordToTestPrinting{:a 1, :b 2}"
             (pr-str r)))
      #_(is (= "#clojure.test-clojure.protocols.RecordToTestPrinting[1, 2]"
             (binding [*print-dup* true] (pr-str r))))
      #_(is (= "#clojure.test-clojure.protocols.RecordToTestPrinting{:a 1, :b 2}"
             (binding [*print-dup* true *verbose-defrecords* true] (pr-str r)))))))

(defrecord RecordToTest__ [__a ___b])
(defrecord TypeToTest__   [__a ___b])

(deftest test-record-and-type-field-names
  (testing "that types and records allow names starting with double-underscore.
            This is a regression test for CLJ-837."
    (let [r (RecordToTest__. 1 2)
          t (TypeToTest__. 3 4)]
      (are [x y] (= x y)
           1 (:__a r)
           2 (:___b r)
           3 (.-__a t)
           4 (.-___b t)))))

(defrecord RecordToTestLongHint [^long a])
(defrecord RecordToTestByteHint [^byte a])
(defrecord RecordToTestBoolHint [^boolean a])
(defrecord RecordToTestCovariantHint [^clojerl.String a]) ;; same for arrays also
(deftype TypeToTestLongHint [^long a])
(deftype TypeToTestByteHint [^byte a])

(deftest hinting-test
  (testing "that primitive hinting requiring no coercion works as expected"
    (is (= (RecordToTestLongHint. 42) #clojure.test-clojure.protocols.RecordToTestLongHint{:a 42}))
    (is (= (RecordToTestLongHint. 42) #clojure.test-clojure.protocols.RecordToTestLongHint[42]))
    (is (= (RecordToTestLongHint. 42) (clojure.test-clojure.protocols.RecordToTestLongHint/create.e {:a 42})))
    (is (= (RecordToTestLongHint. 42) (map->RecordToTestLongHint {:a 42})))
    (is (= (RecordToTestLongHint. 42) (->RecordToTestLongHint 42)))
    (is (= (.-a (TypeToTestLongHint. 42)) (.-a (->TypeToTestLongHint (long 42))))))
  (testing "that primitive hinting requiring coercion works as expected"
    (is (= (RecordToTestByteHint. 42) (clojure.test-clojure.protocols.RecordToTestByteHint/create.e {:a (byte 42)})))
    (is (= (RecordToTestByteHint. 42) (map->RecordToTestByteHint {:a (byte 42)})))
    (is (= (RecordToTestByteHint. 42) (->RecordToTestByteHint (byte 42))))
    (is (= (.-a (TypeToTestByteHint. 42)) (.-a (->TypeToTestByteHint (byte 42))))))
  (testing "that primitive hinting for non-numerics works as expected"
    (is (= (RecordToTestBoolHint. true) #clojure.test-clojure.protocols.RecordToTestBoolHint{:a true}))
    (is (= (RecordToTestBoolHint. true) #clojure.test-clojure.protocols.RecordToTestBoolHint[true]))
    (is (= (RecordToTestBoolHint. true) (clojure.test-clojure.protocols.RecordToTestBoolHint/create.e {:a true})))
    (is (= (RecordToTestBoolHint. true) (map->RecordToTestBoolHint {:a true})))
    (is (= (RecordToTestBoolHint. true) (->RecordToTestBoolHint true))))
  (testing "covariant hints -- deferred"))

; see CLJ-845
(defprotocol SyntaxQuoteTestProtocol
  (sqtp [p]))

(defmacro try-extend-type [c]
  `(extend-type ~c
     SyntaxQuoteTestProtocol
     (sqtp [p#] p#)))

(defmacro try-extend-protocol [c]
  `(extend-protocol SyntaxQuoteTestProtocol
     ~c
     (sqtp [p#] p#)))

(try-extend-type clojerl.String)
(try-extend-protocol clojerl.Keyword)

(deftest test-no-ns-capture
  (is (= "foo" (sqtp "foo")))
  (is (= :foo (sqtp :foo))))

(defprotocol Dasherizer
  (-do-dashed [this]))
(deftype Dashed []
  Dasherizer
  (-do-dashed [this] 10))

(deftest test-leading-dashes
  (is (= 10 (-do-dashed (Dashed.))))
  (is (= [10] (map -do-dashed [(Dashed.)]))))
